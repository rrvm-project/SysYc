WHITESPACE = _{ " " | "\t" | NEWLINE }
NEWLINE = _{ "\r\n" | "\n" | "\r" }
COMMENT = _{
  "//" ~ (!NEWLINE ~ ANY)* |
  "/*" ~ (!"*/" ~ ANY)* ~ "*/"
}

DecimalInteger = _{ "0"|("1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9") ~ ASCII_DIGIT* }
HexDigit=_{"0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"|"a"|"b"|"c"|"d"|"e"|"f"|"A"|"B"|"C"|"D"|"E"|"F"}
HexInteger=_{("0X"|"0x") ~ HexDigit+}
OctalDigit=_{"0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"}
OctalInteger=_{"0" ~ OctalDigit+}
BinaryInteger=_{("0b"|"0B") ~ ("0"|"1")+}
Integer={HexInteger|BinaryInteger|OctalInteger|DecimalInteger}

Decimals = _{ ASCII_DIGIT+ }
Exponent= _{("e"|"E") ~ ("+"|"-")* ~ Decimals }
Float=${ (Decimals ~ "." ~ Decimals? ~ Exponent? ~ ("f"|"F")?)|(Decimals~ Exponent ~ ("f"|"F")?)|("." ~ Decimals ~ Exponent? ~ ("f"|"F")?) }

Number = _{ Float | Integer}

// 怎么能不支持下划线，我给他加上了
Identifier = ${ (ASCII_ALPHA | "_") ~ (ASCII_DIGIT | ASCII_ALPHA | "_")* }

int_t = { "int " }
float_t = { "float " }
void_t = { "void " }
BType = _{ int_t | float_t }
FuncType = _{ int_t | float_t | void_t }

Add = { "+" }
Sub = { "-" }
Mul = { "*" }
Div = { "/" }
Mod = { "%" }
LT = { "<" }
LE = { "<=" }
GT = { ">" }
GE = { ">=" }
EQ = { "==" }
NE = { "!=" }
LAnd = { "&&" }
LOr = { "||" }
Assign = { "=" }

UnaryAdd = { "+" }
UnarySub = { "-" }
UnaryNot = { "!" }
UnaryBitNot = { "~" }

Break = { "break" }
Continue = { "continue" }
Return = { "return" ~ Expr? }

BinaryOp = _{
  Add | Sub | Mul | Div | Mod | LE | LT | GE | GT | EQ | NE | LOr | LAnd | Assign
}
UnaryOp = _{
  UnaryAdd | UnarySub | UnaryNot | UnaryBitNot
}

CompUnit = _{ Decl | FuncDecl }
DimList = { ("[" ~ Expr ~ "]")* }

Decl = { ConstDecl | VarDecl }
ConstDecl = { "const " ~ BType ~ VarDef ~ ("," ~ VarDef)* ~ ";" }
VarDecl = { BType ~ VarDef ~ ("," ~ VarDef)* ~ ";" }
VarDef = { Identifier ~ DimList ~ ("=" ~ InitVal)? }
InitVal = _{ Expr | InitValList }
InitValList = { "{" ~ (InitVal ~ ("," ~ InitVal)*)? ~ "}" }
FuncDecl = {
  FuncType ~ Identifier ~ "(" ~ FormalParams ~ ")" ~ Block
}

FormalParams = { (FormalParam ~ ("," ~ FormalParam)*)? }
FormalParam = { BType ~ Identifier ~ ("[]" ~ DimList)? }
RealParams = _{ (Expr ~ ("," ~ Expr)*)? }

Block = { "{" ~ BlockItem* ~ "}" }
BlockItem = _{ Decl | Stmt }

IfStmt = {
  "if" ~ "(" ~ Expr ~ ")" ~ Stmt ~ ( "else" ~ Stmt)?
}
WhileStmt = {
  "while" ~ "(" ~ Expr ~ ")" ~ Stmt
}

Stmt = {
  ";"
  | Return ~ ";"
  | Continue ~ ";"
  | Break ~ ";"
  | WhileStmt
  | Expr ~ ";"
  | Block
  | IfStmt
}

Lval = { Identifier ~ DimList }
FuncCall = { Identifier ~ "(" ~ RealParams ~ ")"}
Primary = _{ "(" ~ Expr ~ ")" | Number | FuncCall | Lval }
Atom = _{ UnaryOp* ~ Primary }
Expr = { Atom ~ (BinaryOp ~ Atom)* }

Program = _{ SOI ~ (CompUnit)* ~ EOI }